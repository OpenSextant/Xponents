// -*- coding: utf-8 -*-
//                              NOTICE
/**
 *
 *  Copyright 2009-2013 The MITRE Corporation.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * **************************************************************************
 *                          NOTICE
 * This software was produced for the U. S. Government under Contract No.
 * W15P7T-12-C-F600, and is subject to the Rights in Noncommercial Computer
 * Software and Noncommercial Computer Software Documentation Clause
 * 252.227-7014 (JUN 1995)
 *
 * (c) 2012 The MITRE Corporation. All Rights Reserved.
 * **************************************************************************
 */

// ALL Patterns below  - defines, rules, etc. -- are for MATCHING.
// Parsing of actual fields named in defines is done after matches are found.
// Validation of parsed fields is last.

# Well-known month abbreviations.
#DEFINE MON_ABBREV  JAN|FEB|MAR|APR|MAY|JUN|JUL|AUG|SEPT?|OCT|NOV|DEC

# A name starting with 3 ASCII letters as above, but followed by other letters, possibly not English or ASCII.
# Detection of month/day/year patterns with non-English month names is only a coincidence if they share a common prefix.
# Locales for date patterns and language options could be explored further. But is beyond scope.
#DEFINE MON_NAME JAN\w*|FEB\w*|MAR\w*|APR\w*|MAY|JUNE?|JULY?|AUG\w*|SEP\w*|OCT\w*|NOV\w*|DEC\w*
// #DEFINE MON_NAME   [A-Z]{3}\w{0,8}

#  Fixed length fields
// In all practicality, year is 1xxx or 2xxx.  Years 0001 to 0999 not really considered.
#DEFINE YEAR         [12]\d{3}
#DEFINE YY           '?\d\d

//  Year/YY is 2-4 digits,... but could be 3.  This is only used for matching. XTemp still validates matches.
//  '76
//
#DEFINE YEARYY       '?\d{2}|\d{4}
#DEFINE MM           [01]\d 
#DEFINE DD           [0-3]\d
#DEFINE SHORT_TZ     [A-Z]

#DEFINE hh    [0-2]\d
#DEFINE mm    [0-5]\d
#DEFINE ss    [0-5]\d


#   Variable length
#DEFINE DOM         [0-3]?\d
#DEFINE MONTH       [01]?\d
#DEFINE LONG_TZ     [A-Z]{3,5}

#DEFINE OF	of|Of|OF

// ........................................
// Month, Day, Year patterns, MDY
// ........................................
// FORM: DATE: MM/DD/YY 
#RULE   MDY   01    \b<MONTH>/<DOM>/<YY>\b
#TEST   MDY   01    12/30/90
#TEST   MDY   01    DATE: 12/30/90
#TEST   MDY   01    13/30/90    FAIL bad MON
#TEST   MDY   01    12/32/90    FAIL bad DOM
#TEST   MDY   01    12/30/01
#TEST   MDY   01    12/30/00
#TEST   MDY   01    12/30/55
#TEST   MDY   01    12/30/15

// FORM: DATE: MM/DD/YYYY 
#RULE   MDY   02    \b<MONTH>/<DOM>/<YEAR>\b
#TEST   MDY   02    12/30/1990
#TEST   MDY   02    DATE:  12/30/1990
#TEST   MDY   02    13/30/1990    FAIL bad MON
#TEST   MDY   02    12/32/1990    FAIL bad DOM
#TEST   MDY   02    12/30/2001
#TEST   MDY   02    12/30/0000
#TEST   MDY   02    12/30/1955
#TEST   MDY   02    12/30/1915

// FORM: MMM DD, YYYY   or MMM DD YYYY,  MMM DD, YY, etc.
#RULE   MDY   03    \b<MON_NAME>\s+<DOM>[\s,]+<YEARYY>\b
#TEST   MDY   03    DEC 30, 1990
#TEST   MDY   03    DEC 30 1990
#TEST   MDY   03    DEC 30 90
#TEST   MDY   03    DEC 30 990         FAIL bad year
#TEST   MDY   03    DEC 00 1990
#TEST   MDY   03    DEC 01 2300        FAIL ambiguous; time appears to be 2300 where year is expected
#TEST   MDY   03    DECEMBER 30 1990
#TEST   MDY   03    DECMEBER 30 90
#TEST   MDY   03    DECIEMBRE 30 1990
#TEST   MDY   03    DECIEMBRE 00 1990
#TEST   MDY   03    DECEMBER 01 2300   FAIL ambiguous; time appears to be 2300 where year is expected


// FORM: MMM, YYYY  or Month, YYYY   comma optional. 4-digit year required
#RULE   MDY   04    \b<MON_NAME>[\s,]+<YEAR>\b
#TEST   MDY   04    DEC 1990
#TEST   MDY   04    DEC, 1990
#TEST   MDY   04    DECEMBER, 1990
#TEST   MDY   04    DECIEMBRE, 1990

// FORM: MMM of YYYY
#RULE   MDY   04a    \b<MON_NAME>\s+<OF>\s+<YEAR>\b
#TEST   MDY   04a    DEC of 1990
#TEST   MDY   04a    DECEMBER of 1990

#RULE   MDY   05    \b<DOM>\s+<MON_NAME>[\s,]+<YEARYY>\b
#TEST   MDY   05    30 DEC 1990
#TEST   MDY   05    30 DEC 90
#TEST   MDY   05    01 DEC 00
#TEST   MDY   05    01 DEC 02
#TEST   MDY   05    30 DECEMBER 1990
#TEST   MDY   05    30 DECMEBER 1990
#TEST   MDY   05    30 DECIEMBRE 1990


// FORM: DATE:  DD-MON-YYYY
#RULE   DMY   01    \b<DOM>-<MON_NAME>-<YEARYY>\b
#TEST   DMY   01    12-DEC-90
#TEST   DMY   01    12-DEC-1990

// FORM: DATE:  DD MON YYYY
#RULE   DMY   02    \b<DOM>\s*<MON_NAME>\s*<YEARYY>\b
#TEST   DMY   02    12 DEC 90
#TEST   DMY   02    12 DEC 1990
#TEST   DMY   02    12DEC90
#TEST   DMY   02    12DEC1990


// ........................................
// DATE TIME PATTERNS,  DTM
// ........................................

// FORM: A|O|P|R DDHHMMZ MMM YY 
#RULE   DTM   01   \b<DD><hh><mm><SHORT_TZ>\s*<MON_ABBREV>\s*<YY>\b
#TEST   DTM   01   A 301400Z DEC 90
#TEST   DTM   01   R 301400Z DEC 90
#TEST   DTM   01   A 351400Z DEC 90

// FORM: YYYYMMDDTHHMMZ
#RULE   DTM   02   \b<YEAR><MM><DD>T<hh><mm><SHORT_TZ>\b
#TEST   DTM   02   20101230T1400Z

// FORM: YYYY-MM-DDTHH:MM:SS
#RULE   DTM   03   \b<YEAR>-<MM>-<DD>T<hh>:<mm>:<ss>
#TEST   DTM   03   2010-12-30T14:00:00:12

// FORM: YYYY-MM-DD HH:MM:SS
#RULE   DTM   04   \b<YEAR>-<MM>-<DD>\s+<hh>:<mm>:<ss>
#TEST   DTM   04   2010-12-30 14:00:00:12

// FORM: YYYYMMDD
#RULE   DTM   05   \b<YEAR><MM><DD>\b
#TEST   DTM   05   20101230

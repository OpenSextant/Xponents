#  Patterns of Life (Poli)
#  
#  NOTE: These are all examples.  Reference implementations are in poli.data pkg.
#
#  document title grabber
#  telephony patterns (cell, land line)
#  networking and communications (email, IP)
#  Zip codes, common postal patterns
#  
# ...................................
#  Telephony Patterns
# ...................................
#DEFINE tel_ctry \d{3}
#DEFINE tel_area \(?\d{3}\)?
#DEFINE tel_sep [-.\s]
#DEFINE tel_num \d{3}[-\.\s]\d{4}
#DEFINE tel_exchange x\d{2,5}

#CLASS PHONE  org.mitre.opensextant.poli.data.TelephoneNumber
#RULE PHONE  01  <tel_area><tel_sep><tel_num>[\s,]?<tel_exchange>?
#TEST PHONE  01  (978-793-0080)
#TEST PHONE  01  (978)-793-0080
#TEST PHONE  01  c.978.793.0080
#TEST PHONE  01  tel. 978.793.0800 x44

# ...................................
#  Document Patterns
# ...................................

# Title is some text denoted by a SUBJ or a TITLE heading, bound by end of line.
# Ends of multi-line titles are not easy to detect -- add your own if that is the case.
#DEFINE title_marker    SUBJECT|SUBJ|TITLE
#DEFINE title           .+
#RULE TITLE 01 <title_marker>[:/\n\s]+<title>[\n\r]+

#TEST TITLE 01 SUBJ: Lorem ip sip sum cuta tookus blookie.$NL
#TEST TITLE 01 SUBJECT: Lorem ip sip sum cuta tookus blookie.$NL
#TEST TITLE 01 TITLE    Happiness is having nothing$NL 
#TEST TITLE 01 TITLE    :   Happiness is having nothing$NL 

# ...................................
#  Cyber Patterns
# ...................................
#DEFINE byte [\dA-F]{2}
#CLASS CYBER.MAC  org.mitre.opensextant.poli.data.MACAddress
#RULE CYBER.MAC 01 <byte>[-:]<byte>[-:]<byte>[-:]<byte>[-:]<byte>[-:]<byte>
#TEST CYBER.MAC 01 00:45:AF:c0:44:EE
#TEST CYBER.MAC 01 00-45-AF-c0-44-EE



# ...................................
#  Monetary and Financial Patterns
# ...................................
#DEFINE currency_sym        [€£$¥]
#DEFINE currency_nom        usd|euro|yuan|dollars|\$US|pounds
#DEFINE money_magnitude     million|thousand|mil\.?|m|k|b
#DEFINE money_amount        [\d,.]+

// Money Amounts:
//  (a)  number mag currency     80K pounds;   80,700 pounds;  80.700 pounds;
//  (b)  sym number mag         £80K 
// User must apply a LOCALE to parse currency -- does context of document imply European number separators? "." or "," ?
//
#CLASS MONEY.AMT  org.mitre.opensextant.poli.data.Money
#RULE  MONEY.AMT  01sym     <money_amount>\s*<money_magnitude>?\s*<currency_sym>
#TEST  MONEY.AMT  01sym     787K €
#TEST  MONEY.AMT  01sym     78.700K €

#RULE  MONEY.AMT  01nom     <money_amount>\s*<money_magnitude>?\s*<currency_nom>
#TEST  MONEY.AMT  01nom     78.700K USD
#TEST  MONEY.AMT  01nom     78.700 USD
#TEST  MONEY.AMT  01nom     78,700 USD
#TEST  MONEY.AMT  01nom     8 million USD

#RULE  MONEY.AMT  02sym     <currency_sym>\s*<money_amount>\s*<money_magnitude>?
#TEST  MONEY.AMT  02sym     €787K
#TEST  MONEY.AMT  02sym     €78.700K
#TEST  MONEY.AMT  02sym     € 78.700K
#TEST  MONEY.AMT  02sym     € 78.700
